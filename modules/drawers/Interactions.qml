import QtQuick
import QtQuick.Controls
import Quickshell
import Caelestia.Config
import qs.components
import qs.components.controls
import qs.modules.bar as Bar
import qs.modules.bar.popouts as BarPopouts

CustomMouseArea {
    id: root

    required property ShellScreen screen
    required property BarPopouts.Wrapper popouts
    required property DrawerVisibilities visibilities
    required property Panels panels
    required property Bar.BarWrapper bar
    required property real borderThickness
    required property bool fullscreen

    property point dragStart
    property bool dashboardShortcutActive
    property bool osdShortcutActive
    property bool utilitiesShortcutActive

    Timer {
        id: hoverTimer
        interval: GlobalConfig.general.hoverDelay
        property bool showOsd: false
        property bool showDashboard: false
        property bool showLauncher: false
        property bool showUtilities: false
        property bool showBar: false
        property bool showPopout: false
        property string pendingAction: ""
        property real popoutY: -1

        function clear(): void {
            showOsd = false;
            showDashboard = false;
            showLauncher = false;
            showUtilities = false;
            showBar = false;
            showPopout = false;
            pendingAction = "";
            popoutY = -1;
            stop();
        }

        function cancel(action: string): void {
            if (pendingAction === action)
                clear();
        }

        function queue(action: string, y: var): void {
            if (pendingAction !== action) {
                clear();
                pendingAction = action;
                restart();
            }

            showOsd = action === "osd";
            showDashboard = action === "dashboard";
            showLauncher = action === "launcher";
            showUtilities = action === "utilities";
            showBar = action === "bar";
            showPopout = action === "popout";
            popoutY = y ?? -1;
        }

        onTriggered: {
            const x = root.mouseX;
            const y = root.mouseY;

            if (showBar && x < root.bar.implicitWidth) {
                root.bar.isHovered = true;
            }
            if (showPopout && x < root.bar.implicitWidth) {
                root.bar.checkPopout(y);
            }
            if (showOsd && root.shouldShowOsd(x, y)) {
                if (!root.osdShortcutActive) {
                    root.visibilities.osd = true;
                    root.panels.osd.hovered = true;
                } else {
                    root.osdShortcutActive = false;
                    root.panels.osd.hovered = true;
                }
            }
            if (showDashboard && Config.dashboard.showOnHover && root.inTopPanel(root.panels.dashboard, x, y)) {
                if (!root.dashboardShortcutActive) {
                    root.visibilities.dashboard = true;
                } else {
                    root.dashboardShortcutActive = false;
                }
            }
            if (showLauncher && Config.launcher.showOnHover && root.inBottomPanel(root.panels.launcher, x, y)) {
                root.visibilities.launcher = true;
            }
            if (showUtilities && root.inBottomPanel(root.panels.utilities, x, y)) {
                if (!root.utilitiesShortcutActive) {
                    root.visibilities.utilities = true;
                } else {
                    root.utilitiesShortcutActive = false;
                }
            }

            clear();
        }
    }

    function withinPanelHeight(panel: Item, x: real, y: real): bool {
        const panelY = root.borderThickness + panel.y;
        return y >= panelY - Config.border.rounding && y <= panelY + panel.height + Config.border.rounding;
    }

    function withinPanelWidth(panel: Item, x: real, y: real): bool {
        const panelX = bar.implicitWidth + panel.x;
        return x >= panelX - Config.border.rounding && x <= panelX + panel.width + Config.border.rounding;
    }

    function inLeftPanel(panel: Item, x: real, y: real): bool {
        return x < bar.implicitWidth + panel.x + panel.width && withinPanelHeight(panel, x, y);
    }

    function inRightPanel(panel: Item, x: real, y: real): bool {
        return x > Math.min(width - Config.border.minThickness, bar.implicitWidth + panel.x) && withinPanelHeight(panel, x, y);
    }

    function inTopPanel(panel: Item, x: real, y: real): bool {
        const panelHeight = panel.height * (1 - (panel.offsetScale ?? 0)); // qmllint disable missing-property
        return y < Math.max(Config.border.minThickness, Config.border.thickness + panelHeight) && withinPanelWidth(panel, x, y);
    }

    function inBottomPanel(panel: Item, x: real, y: real, isCorner = false): bool {
        const panelHeight = panel.height * (1 - (panel.offsetScale ?? 0)); // qmllint disable missing-property
        return y > height - Math.max(Config.border.minThickness, Config.border.thickness + panelHeight) - (isCorner ? Config.border.rounding : 0) && withinPanelWidth(panel, x, y);
    }

    function shouldShowOsd(x: real, y: real): bool {
        if (panels.sidebar.width === 0)
            return inRightPanel(panels.osd, x, y);

        return x < width - panels.sidebar.width && inRightPanel(panels.osd, x, y);
    }

    function onWheel(event: WheelEvent): void {
        if (fullscreen)
            return;
        if (event.x < bar.implicitWidth) {
            bar.handleWheel(event.y, event.angleDelta);
        }
    }

    anchors.fill: parent
    acceptedButtons: fullscreen ? Qt.NoButton : Qt.AllButtons
    hoverEnabled: true

    onPressed: event => dragStart = Qt.point(event.x, event.y)
    onContainsMouseChanged: {
        if (!containsMouse) {
            hoverTimer.clear();

            // Only hide if not activated by shortcut
            if (!osdShortcutActive) {
                visibilities.osd = false;
                root.panels.osd.hovered = false;
            }

            if (!dashboardShortcutActive)
                visibilities.dashboard = false;

            if (!utilitiesShortcutActive)
                visibilities.utilities = false;

            if (!popouts.currentName.startsWith("traymenu") || ((popouts.current as StackView)?.depth ?? 0) <= 1) {
                popouts.hasCurrent = false;
                bar.closeTray();
            }

            if (Config.bar.showOnHover)
                bar.isHovered = false;
        }
    }

    onPositionChanged: event => {
        if (popouts.isDetached)
            return;

        const x = event.x;
        const y = event.y;
        const dragX = x - dragStart.x;
        const dragY = y - dragStart.y;

        if (fullscreen) {
            root.panels.osd.hovered = inRightPanel(panels.osdWrapper, x, y);
            return;
        }

        // Show bar in non-exclusive mode on hover
        if (Config.bar.showOnHover) {
            if (x < bar.clampedWidth) {
                if (!visibilities.bar && !bar.isHovered && !hoverTimer.showBar) {
                    hoverTimer.queue("bar");
                }
            } else {
                hoverTimer.cancel("bar");
            }
        }

        // Show/hide bar on drag
        if (pressed && dragStart.x < bar.clampedWidth) {
            if (dragX > Config.bar.dragThreshold)
                visibilities.bar = true;
            else if (dragX < -Config.bar.dragThreshold)
                visibilities.bar = false;
        }

        if (panels.sidebar.offsetScale === 1) {
            // Show osd on hover
            const showOsd = shouldShowOsd(x, y);

            if (showOsd) {
                if (!visibilities.osd && !hoverTimer.showOsd) {
                    hoverTimer.queue("osd");
                } else if (visibilities.osd && osdShortcutActive) {
                    osdShortcutActive = false;
                    root.panels.osd.hovered = true;
                }
            } else {
                hoverTimer.cancel("osd");
                if (!osdShortcutActive) {
                    visibilities.osd = false;
                    root.panels.osd.hovered = false;
                }
            }

            const showSidebar = pressed && dragStart.x > Math.min(width - Config.border.minThickness, bar.implicitWidth + panels.sidebar.x);

            // Show/hide session on drag
            if (pressed && inRightPanel(panels.sessionWrapper, dragStart.x, dragStart.y) && withinPanelHeight(panels.sessionWrapper, x, y)) {
                if (dragX < -Config.session.dragThreshold)
                    visibilities.session = true;
                else if (dragX > Config.session.dragThreshold)
                    visibilities.session = false;

                // Show sidebar on drag if in session area and session is nearly fully visible
                if (showSidebar && panels.session.offsetScale <= 0 && dragX < -Config.sidebar.dragThreshold)
                    visibilities.sidebar = true;
            } else if (showSidebar && dragX < -Config.sidebar.dragThreshold) {
                // Show sidebar on drag if not in session area
                visibilities.sidebar = true;
            }
        } else {
            const outOfSidebar = x < width - panels.sidebar.width * (1 - panels.sidebar.offsetScale);
            // Show osd on hover
            const showOsd = shouldShowOsd(x, y);

            if (showOsd) {
                if (!visibilities.osd && !hoverTimer.showOsd) {
                    hoverTimer.queue("osd");
                } else if (visibilities.osd && osdShortcutActive) {
                    osdShortcutActive = false;
                    root.panels.osd.hovered = true;
                }
            } else {
                hoverTimer.cancel("osd");
                if (!osdShortcutActive) {
                    visibilities.osd = false;
                    root.panels.osd.hovered = false;
                }
            }

            // Show/hide session on drag
            if (pressed && outOfSidebar && inRightPanel(panels.sessionWrapper, dragStart.x, dragStart.y) && withinPanelHeight(panels.sessionWrapper, x, y)) {
                if (dragX < -Config.session.dragThreshold)
                    visibilities.session = true;
                else if (dragX > Config.session.dragThreshold)
                    visibilities.session = false;
            }

            // Hide sidebar on drag
            if (pressed && inRightPanel(panels.sidebar, dragStart.x, 0) && dragX > Config.sidebar.dragThreshold)
                visibilities.sidebar = false;
        }

        // Show launcher on hover, or show/hide on drag if hover is disabled
        if (Config.launcher.showOnHover) {
            if (inBottomPanel(panels.launcher, x, y)) {
                if (!visibilities.launcher && !hoverTimer.showLauncher) {
                    hoverTimer.queue("launcher");
                }
            } else {
                hoverTimer.cancel("launcher");
            }
        } else if (pressed && inBottomPanel(panels.launcher, dragStart.x, dragStart.y) && withinPanelWidth(panels.launcher, x, y)) {
            if (dragY < -Config.launcher.dragThreshold)
                visibilities.launcher = true;
            else if (dragY > Config.launcher.dragThreshold)
                visibilities.launcher = false;
        }

        // Show dashboard on hover
        const showDashboard = Config.dashboard.showOnHover && inTopPanel(panels.dashboard, x, y);

        if (showDashboard) {
            if (!visibilities.dashboard && !hoverTimer.showDashboard) {
                hoverTimer.queue("dashboard");
            } else if (visibilities.dashboard && dashboardShortcutActive) {
                dashboardShortcutActive = false;
            }
        } else {
            hoverTimer.cancel("dashboard");
            if (!dashboardShortcutActive) {
                visibilities.dashboard = false;
            }
        }

        // Show/hide dashboard on drag (for touchscreen devices)
        if (pressed && inTopPanel(panels.dashboard, dragStart.x, dragStart.y) && withinPanelWidth(panels.dashboard, x, y)) {
            if (dragY > Config.dashboard.dragThreshold)
                visibilities.dashboard = true;
            else if (dragY < -Config.dashboard.dragThreshold)
                visibilities.dashboard = false;
        }

        // Show utilities on hover
        const showUtilities = inBottomPanel(panels.utilities, x, y, true);

        if (showUtilities) {
            if (!visibilities.utilities && !hoverTimer.showUtilities) {
                hoverTimer.queue("utilities");
            } else if (visibilities.utilities && utilitiesShortcutActive) {
                utilitiesShortcutActive = false;
            }
        } else {
            hoverTimer.cancel("utilities");
            if (!utilitiesShortcutActive) {
                visibilities.utilities = false;
            }
        }

        // Show popouts on hover
        if (x < bar.implicitWidth) {
            if (!hoverTimer.showPopout) {
                hoverTimer.queue("popout", y);
            } else {
                hoverTimer.popoutY = y;
            }
        } else {
            hoverTimer.cancel("popout");
            if ((!popouts.currentName.startsWith("traymenu") || ((popouts.current as StackView)?.depth ?? 0) <= 1) && !inLeftPanel(panels.popoutsWrapper, x, y)) {
                popouts.hasCurrent = false;
                bar.closeTray();
            }
        }
    }

    // Monitor individual visibility changes
    Connections {
        function onLauncherChanged() {
            // If launcher is hidden, clear shortcut flags for dashboard and OSD
            if (!root.visibilities.launcher) {
                root.dashboardShortcutActive = false;
                root.osdShortcutActive = false;
                root.utilitiesShortcutActive = false;

                // Also hide dashboard and OSD if they're not being hovered
                const inDashboardArea = root.inTopPanel(root.panels.dashboard, root.mouseX, root.mouseY);
                const inOsdArea = root.inRightPanel(root.panels.osdWrapper, root.mouseX, root.mouseY);

                if (!inDashboardArea) {
                    root.visibilities.dashboard = false;
                }
                if (!inOsdArea) {
                    root.visibilities.osd = false;
                    root.panels.osd.hovered = false;
                }
            }
        }

        function onDashboardChanged() {
            if (root.visibilities.dashboard) {
                // Dashboard became visible, immediately check if this should be shortcut mode
                const inDashboardArea = root.inTopPanel(root.panels.dashboard, root.mouseX, root.mouseY);
                if (!inDashboardArea) {
                    root.dashboardShortcutActive = true;
                }
            } else {
                // Dashboard hidden, clear shortcut flag
                root.dashboardShortcutActive = false;
            }
        }

        function onOsdChanged() {
            if (root.visibilities.osd) {
                // OSD became visible, immediately check if this should be shortcut mode
                const inOsdArea = root.inRightPanel(root.panels.osdWrapper, root.mouseX, root.mouseY);
                if (!inOsdArea) {
                    root.osdShortcutActive = true;
                }
            } else {
                // OSD hidden, clear shortcut flag
                root.osdShortcutActive = false;
            }
        }

        function onUtilitiesChanged() {
            if (root.visibilities.utilities) {
                // Utilities became visible, immediately check if this should be shortcut mode
                const inUtilitiesArea = root.inBottomPanel(root.panels.utilities, root.mouseX, root.mouseY);
                if (!inUtilitiesArea) {
                    root.utilitiesShortcutActive = true;
                }
            } else {
                // Utilities hidden, clear shortcut flag
                root.utilitiesShortcutActive = false;
            }
        }

        target: root.visibilities
    }
}
